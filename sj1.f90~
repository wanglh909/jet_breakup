

subroutine values_in_sj(m,i,j)
  use kind
  use data
  use Ldata

  implicit none
  integer(kind=ik):: m,i,j, k, l, n

  do k = 1, 3, 1          !relate to a(3) (the value of si for gaussian_quadrature)
     do l = 1, 3, 1           !relate to a(3) (the value of eta for gaussian_quadrature)

        !Actually, these terms are (k,l,i,j), but can be refurbished in each cycle of i&j, so just need (k,l)
        !s_orth_r(k,l,i,j)
        s_orth_r(k,l) = 1.0_rk / ( reta(k,l)**2 + zeta(k,l)**2 ) * ( rsi(k,l)*phisi(k,l,j)/s_orth(k,l) &
             - reta(k,l)*phieta(k,l,j)*s_orth(k,l) )

        !s_orth_z(k,l,i,j)
        s_orth_z(k,l) = 1.0_rk / ( reta(k,l)**2 + zeta(k,l)**2 ) * ( zsi(k,l)*phisi(k,l,j)/s_orth(k,l) &
             - zeta(k,l)*phieta(k,l,j)*s_orth(k,l) )

        !Aterm_r(k,l,i,j)
        Aterm_r(k,l) = 2.0_rk*reta(k,l)*phieta(k,l,j)*phisi(k,l,i) &
             - ( rsi(k,l)*phieta(k,l,j) + reta(k,l)*phisi(k,l,j) )*phieta(k,l,i)

        !Aterm_z(k,l,i,j)
        Aterm_z(k,l) = 2.0_rk*zeta(k,l)*phieta(k,l,j)*phisi(k,l,i) &
             - ( zsi(k,l)*phieta(k,l,j) + zeta(k,l)*phisi(k,l,j) )*phieta(k,l,i)

        !Bterm_r(k,l,i,j)
        Bterm_r(k,l) = - ( reta(k,l)*phisi(k,l,j) + rsi(k,l)*phieta(k,l,j) )*phisi(k,l,i) &
             + 2.0_rk*rsi(k,l)*phisi(k,l,j)*phieta(k,l,i) 

        !Bterm_z(k,l,i,j)
        Bterm_z(k,l) = - ( zeta(k,l)*phisi(k,l,j) + zsi(k,l)*phieta(k,l,j) )*phisi(k,l,i) &
             + 2.0_rk*zsi(k,l)*phisi(k,l,j)*phieta(k,l,i) 

        !Jp_r((k,l,i,j)
        Jp_r(k,l) = phisi(k,l,j)*zeta(k,l) - phieta(k,l,j)*zsi(k,l)

        !Jp_r((k,l,i,j)
        Jp_z(k,l) = rsi(k,l)*phieta(k,l,j) - reta(k,l)*phisi(k,l,j)

        !rJp_r(k,l,i,j)
        rJp_r(k,l) = phi(k,l,j)*Jp(k,l) + rintfac(k,l)*Jp_r(k,l)


        do n = 1, 9            !phir_r(k,l,n,j)
           phir_r(k,l,n) = -1.0_rk/Jp(k,l)**2 *Jp_r(k,l)*( phisi(k,l,n)*zeta(k,l) - phieta(k,l,n)*zsi(k,l) )     !d phir(n) / d rj

           phir_z(k,l,n) = -1.0_rk/Jp(k,l)**2 *Jp_z(k,l)*( phisi(k,l,n)*zeta(k,l) - phieta(k,l,n)*zsi(k,l) ) + &
                1.0_rk/Jp(k,l)*( phisi(k,l,n)*phieta(k,l,j) - phieta(k,l,n)*phisi(k,l,j) )

           phiz_r(k,l,n) = -1.0_rk/Jp(k,l)**2 *Jp_r(k,l)*( -phisi(k,l,n)*reta(k,l) + phieta(k,l,n)*rsi(k,l) ) + &
                1.0_rk/Jp(k,l)*( -phisi(k,l,n)*phieta(k,l,j) + phieta(k,l,n)*phisi(k,l,j) )

           phiz_z(k,l,n) = -1.0_rk/Jp(k,l)**2 *Jp_z(k,l)*( -phisi(k,l,n)*reta(k,l) + phieta(k,l,n)*rsi(k,l) )
        end do


        !urintfac_r(k,l,j)
        urintfac_r(k,l) = 0.0_rk
        urintfac_z(k,l) = 0.0_rk
        uzintfac_r(k,l) = 0.0_rk
        uzintfac_z(k,l) = 0.0_rk
        vrintfac_r(k,l) = 0.0_rk
        vrintfac_z(k,l) = 0.0_rk
        vzintfac_r(k,l) = 0.0_rk
        vzintfac_z(k,l) = 0.0_rk
        do n = 1, 9
           urintfac_r(k,l) = urintfac_r(k,l) + ulocal(n)*phir_r(k,l,n)
           urintfac_z(k,l) = urintfac_z(k,l) + ulocal(n)*phir_z(k,l,n)
           uzintfac_r(k,l) = uzintfac_r(k,l) + ulocal(n)*phiz_r(k,l,n)
           uzintfac_z(k,l) = uzintfac_z(k,l) + ulocal(n)*phiz_z(k,l,n)
           vrintfac_r(k,l) = vrintfac_r(k,l) + vlocal(n)*phir_r(k,l,n)
           vrintfac_z(k,l) = vrintfac_z(k,l) + vlocal(n)*phir_z(k,l,n)
           vzintfac_r(k,l) = vzintfac_r(k,l) + vlocal(n)*phiz_r(k,l,n)
           vzintfac_z(k,l) = vzintfac_z(k,l) + vlocal(n)*phiz_z(k,l,n)
        end do



     end do
  end do            !end loop for k&l


end subroutine values_in_sj




subroutine VI_in_sj(m,i,j, sj, LNVar, LNOPP)
  use kind
  use data
  use Ldata
  use NOP_mod, only: gaussian_quadrature

  implicit none
  integer(kind=ik):: k, l
  real(kind=rk):: integrandRsi_r_V(3,3), integrandRsi_z_V(3,3), integrandReta_r_V(3,3), integrandReta_z_V(3,3)
  real(kind=rk):: integrandRu_r_V(3,3), integrandRu_z_V(3,3), integrandRu_u(3,3), integrandRuv(3,3), integrandRu_p(3,3)
  real(kind=rk):: integrandRv_r_V(3,3), integrandRv_z_V(3,3), integrandRv_u(3,3), integrandRvv(3,3),integrandRv_p(3,3) 
  real(kind=rk):: integrandRp_r(3,3), integrandRp_z(3,3), integrandRp_u(3,3), integrandRp_v(3,3)
    !? I want to change 3 to Ng: number of gausspoints

  integer(kind=ik):: m,i,j
  integer(kind=ik):: LNVar, LNOPP(9)
  real(kind=rk):: sj(LNVar, LNVar)

  !define integrand(k,l,p,q):
  do k = 1, 3, 1          !relate to a(3) (the value of si for gaussian_quadrature)
     do l = 1, 3, 1           !relate to a(3) (the value of eta for gaussian_quadrature)

        !integrandRsi_u,v,p = integrandReta_u,v,p = 0.0_rk

        integrandRsi_r_V(k,l) = s_orth_r(k,l) * Aterm(k,l) / Jp(k,l)  +  &
             ( s_orth(k,l) + epss ) * Aterm_r(k,l) / Jp(k,l)  &
             -  ( s_orth(k,l) + epss ) * Aterm(k,l) / Jp(k,l)**2 * Jp_r(k,l)  &
             -  eps1 * phisi(k,l,i) * fsi_size(m) / ( rsi(k,l)**2 + zsi(k,l)**2 ) &
             * 2.0_rk*rsi(k,l) * phisi(k,l,j)

        integrandRsi_z_V(k,l) = s_orth_z(k,l) * Aterm(k,l) / Jp(k,l)  +  &
             ( s_orth(k,l) + epss ) * Aterm_z(k,l) / Jp(k,l)  &
             -  ( s_orth(k,l) + epss ) * Aterm(k,l) / (Jp(k,l)**2) * Jp_z(k,l)  &
             -  eps1 * phisi(k,l,i) * fsi_size(m) / ( rsi(k,l)**2 + zsi(k,l)**2 ) &
             * 2.0_rk*zsi(k,l) * phisi(k,l,j) 

        integrandReta_r_V(k,l) = -1.0_rk/(s_orth(k,l)**2) * s_orth_r(k,l) * Bterm(k,l) / Jp(k,l)  &
             +  ( 1.0_rk/s_orth(k,l) + epss ) * Bterm_r(k,l) / Jp(k,l)  &
             -  ( 1.0_rk/s_orth(k,l) + epss ) * Bterm(k,l) / (Jp(k,l)**2) * Jp_r(k,l)  &
             -  eps2 * phieta(k,l,i) * geta_size(m) / ( reta(k,l)**2 + zeta(k,l)**2 ) &
             * 2.0_rk*reta(k,l) * phieta(k,l,j)

        integrandReta_z_V(k,l) =  -1.0_rk/s_orth(k,l)**2 * s_orth_z(k,l) * Bterm(k,l) / Jp(k,l)  &
             +  ( 1.0_rk/s_orth(k,l) + epss ) * Bterm_z(k,l) / Jp(k,l)  &
             -  ( 1.0_rk/s_orth(k,l) + epss ) * Bterm(k,l) / (Jp(k,l)**2) * Jp_z(k,l)  &
             -  eps2 * phieta(k,l,i) * geta_size(m) / ( reta(k,l)**2 + zeta(k,l)**2 ) &
             * 2.0_rk*zeta(k,l) * phieta(k,l,j)


        ! inertial = 
        ! viscous = 
        ! integrandRu_r(k,l) = inertial + viscous
        integrandRu_r_V(k,l) = ( phi(k,l,i)* ( -CTJ*phi(k,l,j)/dt*urintfac(k,l) + &
             ( uintfac(k,l) - rdotintfac(k,l) )*urintfac_r(k,l) + ( vintfac(k,l)- zdotintfac(k,l) )*uzintfac_r(k,l) ) - &
             Kdi*pintfac(k,l)*Oh*( phir_r(k,l,i) - phi(k,l,i)/rintfac(k,l)**2 *phi(k,l,j) ) + &
             Oh*( 2.0_rk*urintfac_r(k,l)*phir(k,l,i) + 2.0_rk*urintfac(k,l)*phir_r(k,l,i) + &
             ( uzintfac_r(k,l) + vrintfac_r(k,l) )*phiz(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phiz_r(k,l,i) - &
             4.0_rk*phi(k,l,i)*phi(k,l,j)/rintfac(k,l)**3 *uintfac(k,l) ) ) *rintfac(k,l)*abs(Jp(k,l)) + &
             
             ( phi(k,l,i)*( udotintfac(k,l) + ( uintfac(k,l) - rdotintfac(k,l) )*urintfac(k,l) +  &
             ( vintfac(k,l) - zdotintfac(k,l) )*uzintfac(k,l) )  - &
             Kdi*pintfac(k,l)*Oh*( phir(k,l,i) + phi(k,l,i)/rintfac(k,l) ) + &
             Oh*( 2.0_rk*urintfac(k,l)*phir(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phiz(k,l,i) + &
             phi(k,l,i)*2.0_rk/rintfac(k,l)**2 *uintfac(k,l) ) ) *rJp_r(k,l) 


        integrandRu_z_V(k,l) = ( phi(k,l,i)* ( ( uintfac(k,l) - rdotintfac(k,l) )*urintfac_z(k,l) - &
             CTJ*phi(k,l,j)/dt*uzintfac(k,l) + ( vintfac(k,l)- zdotintfac(k,l) )*uzintfac_z(k,l) ) - &
             Kdi*pintfac(k,l)*Oh*phir_z(k,l,i) + &
             Oh*( 2.0_rk*urintfac_z(k,l)*phir(k,l,i) + 2.0_rk*urintfac(k,l)*phir_z(k,l,i) + &
             ( uzintfac_z(k,l) + vrintfac_z(k,l) )*phiz(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phiz_z(k,l,i) ) ) &
             *rintfac(k,l)*abs(Jp(k,l)) + &
             
             ( phi(k,l,i)*( udotintfac(k,l) + ( uintfac(k,l) - rdotintfac(k,l) )*urintfac(k,l) +  &
             ( vintfac(k,l) - zdotintfac(k,l) )*uzintfac(k,l) )  - &
             Kdi*pintfac(k,l)*Oh*( phir(k,l,i) + phi(k,l,i)/rintfac(k,l) ) + &
             Oh*( 2.0_rk*urintfac(k,l)*phir(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phiz(k,l,i) + &
             phi(k,l,i)*2.0_rk/rintfac(k,l)**2 *uintfac(k,l) ) )  *rintfac(k,l)* Jp_z(k,l)



        integrandRu_u(k,l) = ( phi(k,l,i)* ( CTJ*phi(k,l,j)/dt + phi(k,l,j)*urintfac(k,l) +  &
             ( uintfac(k,l) - rdotintfac(k,l) )*phir(k,l,j) + ( vintfac(k,l)- zdotintfac(k,l) )*phiz(k,l,j) ) + &
             Oh*( 2.0_rk*phir(k,l,i)*phir(k,l,j) + phiz(k,l,i)*phiz(k,l,j) + 2.0_rk/rintfac(k,l)**2 *phi(k,l,i)*phi(k,l,j) ) ) &
             *rintfac(k,l)*abs(Jp(k,l))  

        integrandRuv(k,l) = ( phi(k,l,i)*phi(k,l,j)*uzintfac(k,l) + Oh*phir(k,l,j)*phiz(k,l,i) ) *rintfac(k,l)*abs(Jp(k,l)) 

        integrandRu_p(k,l) = -Kdi*psi(k,l,j)*Oh*( phir(k,l,i) + phi(k,l,i)/rintfac(k,l) ) *rintfac(k,l)*abs(Jp(k,l))
        !j=2,4,5,6,8,  integrandRu_p(k,l) = 0.0_rk




        integrandRv_r_V(k,l) = ( phi(k,l,i)* ( -CTJ*phi(k,l,j)/dt*vrintfac(k,l) + &
             ( uintfac(k,l) - rdotintfac(k,l) )*vrintfac_r(k,l) + ( vintfac(k,l)- zdotintfac(k,l) )*vzintfac_r(k,l) ) - &
             Kdi*pintfac(k,l)*Oh*phiz_r(k,l,i) + &
             Oh*( ( uzintfac_r(k,l) + vrintfac_r(k,l) )*phir(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phir_r(k,l,i) + &
             2.0_rk*vzintfac_r(k,l)*phiz(k,l,i) + 2.0_rk*vzintfac(k,l)*phiz_r(k,l,i) ) ) *rintfac(k,l)*abs(Jp(k,l)) + &
             
             ( phi(k,l,i)*( vdotintfac(k,l) + ( uintfac(k,l) - rdotintfac(k,l) )*vrintfac(k,l) + &
             ( vintfac(k,l) - zdotintfac(k,l) )*vzintfac(k,l) + Grav ) - Kdi*pintfac(k,l)*Oh*phiz(k,l,i) + &
             Oh*( ( uzintfac(k,l) + vrintfac(k,l) ) *phir(k,l,i) + 2.0_rk*vzintfac(k,l)*phiz(k,l,i) ) ) *rJp_r(k,l) 


        integrandRv_z_V(k,l) = ( phi(k,l,i)* ( ( uintfac(k,l) - rdotintfac(k,l) )*vrintfac_z(k,l) - &
             CTJ*phi(k,l,j)/dt*vzintfac(k,l) + ( vintfac(k,l)- zdotintfac(k,l) )*vzintfac_z(k,l) ) - &
             Kdi*pintfac(k,l)*Oh*phiz_z(k,l,i) + &
             Oh*( ( uzintfac_z(k,l) + vrintfac_z(k,l) )*phir(k,l,i) + ( uzintfac(k,l) + vrintfac(k,l) )*phir_z(k,l,i) + &
             2.0_rk*vzintfac_z(k,l)*phiz(k,l,i) + 2.0_rk*vzintfac(k,l)*phiz_z(k,l,i) ) ) *rintfac(k,l)*abs(Jp(k,l)) + &
             
             ( phi(k,l,i)*( vdotintfac(k,l) + ( uintfac(k,l) - rdotintfac(k,l) )*vrintfac(k,l) + &
             ( vintfac(k,l) - zdotintfac(k,l) )*vzintfac(k,l) + Grav ) - Kdi*pintfac(k,l)*Oh*phiz(k,l,i) + &
             Oh*( ( uzintfac(k,l) + vrintfac(k,l) ) *phir(k,l,i) + 2.0_rk*vzintfac(k,l)*phiz(k,l,i) ) ) *rintfac(k,l)* Jp_z(k,l)


        integrandRv_u(k,l) = ( phi(k,l,i)*phi(k,l,j)*vrintfac(k,l) + Oh*phiz(k,l,j)*phir(k,l,i) ) *rintfac(k,l)*abs(Jp(k,l)) 

        integrandRvv(k,l) = ( phi(k,l,i)*( CTJ*phi(k,l,j)/dt + ( uintfac(k,l) - rdotintfac(k,l) )*phir(k,l,j) + &
             phi(k,l,j)*vzintfac(k,l) + (vintfac(k,l) - zdotintfac(k,l) )*phiz(k,l,j) ) + &
             Oh*( phir(k,l,i)*phir(k,l,j) + 2.0_rk*phiz(k,l,i)*phiz(k,l,j) ) ) *rintfac(k,l)*abs(Jp(k,l)) 


        integrandRv_p(k,l) = -Kdi*psi(k,l,j)*Oh*phiz(k,l,i) *rintfac(k,l)*abs(Jp(k,l))  




        integrandRp_r(k,l) = psi(k,l,i)* ( urintfac_r(k,l) - uintfac(k,l)/rintfac(k,l)**2 *phi(k,l,j) + vzintfac_r(k,l) ) &
             *rintfac(k,l)*abs(Jp(k,l)) + &
             
             psi(k,l,i)* ( urintfac(k,l) + uintfac(k,l)/rintfac(k,l) + vzintfac(k,l) ) *rJp_r(k,l)


        integrandRp_z(k,l) = psi(k,l,i)* ( urintfac_z(k,l) + vzintfac_z(k,l) ) *rintfac(k,l)*abs(Jp(k,l)) + &
             
             psi(k,l,i)* ( urintfac(k,l) + uintfac(k,l)/rintfac(k,l) + vzintfac(k,l) ) *rintfac(k,l)* Jp_z(k,l)


        integrandRp_u(k,l) = ( phi(k,l,j)/rintfac(k,l) + phir(k,l,j) )*psi(k,l,i) *rintfac(k,l)*abs(Jp(k,l))

        integrandRp_v(k,l) = phiz(k,l,j)*psi(k,l,i) *rintfac(k,l)*abs(Jp(k,l)) 




     end do
  end do   !end loop for k&l, preparation for gaussian_quadrature



  sj(i,j,NNr,NNr) = gaussian_quadrature(integrandRsi_r_V)          ! sjRsi_r(i,j)   
  sj(i,j,NNr,NNz) = gaussian_quadrature(integrandRsi_z_V)          ! sjRsi_z(i,j)  
  sj(i,j,NNr,NNu) = 0.0_rk                                          ! sjRsi_u(i,j)   
  sj(i,j,NNr,NNv) = 0.0_rk                                          ! sjRsi_v(i,j)  
  sj(i,j,NNr,NNp) = 0.0_rk                                          ! sjRsi_p(i,j)  

  sj(i,j,NNz,NNr) = gaussian_quadrature(integrandReta_r_V)           ! sjReta_r(i,j)   
  sj(i,j,NNz,NNz) = gaussian_quadrature(integrandReta_z_V)           ! sjReta_z(i,j)  
  sj(i,j,NNz,NNu) = 0.0_rk                                          ! sjReta_u(i,j)   
  sj(i,j,NNz,NNv) = 0.0_rk                                          ! sjReta_v(i,j)  
  sj(i,j,NNz,NNp) = 0.0_rk                                          ! sjReta_p(i,j)  

  sj(i,j,NNu,NNr) = gaussian_quadrature(integrandRu_r_V)           ! sjRur(i,j)   
  sj(i,j,NNu,NNz) = gaussian_quadrature(integrandRu_z_V)           ! sjRuz(i,j) 
  sj(i,j,NNu,NNu) = gaussian_quadrature(integrandRu_u)           ! sjRuu(i,j)   
  sj(i,j,NNu,NNv) = gaussian_quadrature(integrandRuv)           ! sjRuv(i,j)  
  sj(i,j,NNu,NNp) = gaussian_quadrature(integrandRu_p)           ! sjRup(i,j) 

  sj(i,j,NNv,NNr) = gaussian_quadrature(integrandRv_r_V)           ! sjRvr(i,j)   
  sj(i,j,NNv,NNz) = gaussian_quadrature(integrandRv_z_V)           ! sjRvz(i,j) 
  sj(i,j,NNv,NNu) = gaussian_quadrature(integrandRv_u)           ! sjRvu(i,j)   
  sj(i,j,NNv,NNv) = gaussian_quadrature(integrandRvv)           ! sjRvv(i,j)  
  sj(i,j,NNv,NNp) = gaussian_quadrature(integrandRv_p)           ! sjRvp(i,j) 

  sj(i,j,NNp,NNr) = gaussian_quadrature(integrandRp_r)           ! sjRpr(i,j)   
  sj(i,j,NNp,NNz) = gaussian_quadrature(integrandRp_z)           ! sjRpz(i,j) 
  sj(i,j,NNp,NNu) = gaussian_quadrature(integrandRp_u)           ! sjRpu(i,j)   
  sj(i,j,NNp,NNv) = gaussian_quadrature(integrandRp_v)           ! sjRpv(i,j)  
  sj(i,j,NNp,NNp) = 0.0_rk                                        ! sjRpp(i,j) 



end subroutine VI_in_sj




subroutine SI_in_sj(m,i,j, sj)                     !adding SI to sj
  use kind
  use data
  use Ldata
  use NOP_mod, only: gaussian_quadrature_1d

  implicit none
  integer(kind=ik):: k, ipp, jpp
  real(kind=rk):: integrandRsi_r_S(3), integrandRsi_z_S(3), integrandReta_r_S(3), integrandReta_z_S(3)
  real(kind=rk):: integrandRsi_u_S(3), integrandRsi_v_S(3), &
       integrandRu_r_S(3), integrandRu_z_S(3), integrandRv_r_S(3), integrandRv_z_S(3)

  integer(kind=ik):: m,i,j
  real(kind=rk):: sj(bas,bas,NNVar,NNVar)


  if( (rowNM(m).eq.1) .and. ( (i.eq.1) .or. (i.eq.2) .or. (i.eq.3) ) .and. ( (j.eq.1) .or. (j.eq.2) .or. (j.eq.3) ) ) then
     do k = 1, 3, 1    !three gausspoints
        ipp = i  !phix_1d(k,ipp)
        jpp = j  !phix_1d(k,jpp)
        integrandRsi_r_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( rsi_down(k)**2 + zsi_down(k)**2 ) &
             * 2.0_rk*rsi_down(k) * phix_1d(k,jpp)
        integrandRsi_z_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( rsi_down(k)**2 + zsi_down(k)**2 ) &
             * 2.0_rk*zsi_down(k) * phix_1d(k,jpp)
     end do
     sj(i,j,NNr,NNr) = sj(i,j,NNr,NNr) - M1*gaussian_quadrature_1d(integrandRsi_r_S)
     sj(i,j,NNr,NNz) = sj(i,j,NNr,NNz) - M1*gaussian_quadrature_1d(integrandRsi_z_S)
  end if

  if( (rowNM(m).eq.NEY) .and. ( (i.eq.7) .or. (i.eq.8) .or. (i.eq.9) ) .and. ( (j.eq.7) .or. (j.eq.8) .or. (j.eq.9) ) ) then
     do k = 1, 3, 1    !three gausspoints
        ipp = i - 6  !phix_1d(k,l)
        jpp = j - 6  !phix_1d(k,jpp)
        integrandRsi_r_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( rsi_up(k)**2 + zsi_up(k)**2 ) &
             * 2.0_rk*rsi_up(k) * phix_1d(k,jpp)
        integrandRsi_z_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( rsi_up(k)**2 + zsi_up(k)**2 ) &
             * 2.0_rk*zsi_up(k) * phix_1d(k,jpp)
     end do
     sj(i,j,NNr,NNr) = sj(i,j,NNr,NNr) - M1*gaussian_quadrature_1d(integrandRsi_r_S)
     sj(i,j,NNr,NNz) = sj(i,j,NNr,NNz) - M1*gaussian_quadrature_1d(integrandRsi_z_S)
  end if

  if( (columnNM(m).eq.1) .and. ( (i.eq.1) .or. (i.eq.4) .or. (i.eq.7) ) .and. ( (j.eq.1) .or. (j.eq.4) .or. (j.eq.7) ) ) then
     do k = 1, 3, 1    !three gausspoints
        ipp = i/3 + 1  !phix_1d(k,ipp)
        jpp = j/3 + 1  !phix_1d(k,jpp)
        integrandReta_r_S(k) = phix_1d(k,ipp) * geta_size(m) / ( reta_left(k)**2 + zeta_left(k)**2 ) &
             * 2.0_rk*reta_left(k) * phix_1d(k,jpp)
        integrandReta_z_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( reta_left(k)**2 + zeta_left(k)**2 ) &
             * 2.0_rk*zeta_left(k) * phix_1d(k,jpp)
     end do
     sj(i,j,NNz,NNr) = sj(i,j,NNz,NNr) - M1*gaussian_quadrature_1d(integrandReta_r_S)
     sj(i,j,NNz,NNz) = sj(i,j,NNz,NNz) - M1*gaussian_quadrature_1d(integrandReta_z_S)
  end if



  if( (columnNM(m).eq.NEX) .and. ( (i.eq.3) .or. (i.eq.6) .or. (i.eq.9) ) )  then      

     if( (j.eq.3) .or. (j.eq.6) .or. (j.eq.9) ) then
        do k = 1, 3, 1    !three gausspoints
           ipp = i/3  !phix_1d(k,l)
           jpp = j/3  !phix_1d(k,l)
           integrandReta_r_S(k) = phix_1d(k,ipp) * geta_size(m) / ( reta_right(k)**2 + zeta_right(k)**2 ) &
                * 2.0_rk*reta_right(k) * phix_1d(k,jpp)
           integrandReta_z_S(k) = phix_1d(k,ipp) * fsi_size(m) / ( reta_right(k)**2 + zeta_right(k)**2 ) &
                * 2.0_rk*zeta_right(k) * phix_1d(k,jpp)


           integrandRsi_r_S(k) = phi_1d(k,ipp)*( zeta_right(k)*CTJ/dt*phi_1d(k,jpp) + &
                phix_1d(k,jpp)*( vintfac_right(k) - zdotintfac_right(k) ) )*rintfac_right(k) + &
                
                phi_1d(k,ipp)*( -zeta_right(k)*( uintfac_right(k) - rdotintfac_right(k) ) + &
                reta_right(k)*( vintfac_right(k) - zdotintfac_right(k) ) )*phi_1d(k,jpp)

           integrandRsi_z_S(k) = phi_1d(k,ipp)*( -phix_1d(k,jpp)*( uintfac_right(k) - rdotintfac_right(k) ) - &
                reta_right(k)*CTJ/dt*phi_1d(k,jpp) )*rintfac_right(k)

           integrandRsi_u_S(k) = -phi_1d(k,jpp)*phi_1d(k,ipp)*zeta_right(k)*rintfac_right(k)

           integrandRsi_v_S(k) = phi_1d(k,jpp)*phi_1d(k,ipp)*reta_right(k)*rintfac_right(k)


           integrandRu_r_S(k) = ( phix_1d(k,ipp)*phix_1d(k,jpp) / ( reta_right(k)**2 + zeta_right(k)**2 ) - &
                reta_right(k)*phix_1d(k,ipp) / ( reta_right(k)**2 + zeta_right(k)**2 )**2 * 2.0_rk*reta_right(k)*phix_1d(k,jpp) - &
                1.0_rk/rintfac_right(k)**2 *phi_1d(k,jpp)*phi_1d(k,ipp) ) * &
                rintfac_right(k)*( reta_right(k)**2 + zeta_right(k)**2 )**0.5_rk + &
                
                ( reta_right(k)*phix_1d(k,ipp) / ( reta_right(k)**2 + zeta_right(k)**2 ) + phi_1d(k,ipp)/rintfac_right(k) )&
                *( phi_1d(k,jpp)*( reta_right(k)**2 + zeta_right(k)**2 )**0.5_rk + &
                rintfac_right(k) / ( reta_right(k)**2 + zeta_right(k)**2 )**0.5_rk *reta_right(k)*phix_1d(k,jpp) )

           integrandRu_z_S(k) = -reta_right(k)*phix_1d(k,ipp)*( reta_right(k)**2 + zeta_right(k)**2 )**(-1.5_rk) *&
                2.0_rk*zeta_right(k)*phix_1d(k,jpp)*rintfac_right(k) + &
                
                ( reta_right(k)*phix_1d(k,ipp) / ( reta_right(k)**2 + zeta_right(k)**2 ) + phi_1d(k,ipp)/rintfac_right(k) ) *&
                rintfac_right(k) / ( reta_right(k)**2 + zeta_right(k)**2 )**0.5_rk *zeta_right(k)*phix_1d(k,jpp)


           integrandRv_r_S(k) = zeta_right(k)*phix_1d(k,ipp)*&
                ( -( reta_right(k)**2 + zeta_right(k)**2 )**(-1.5_rk) *reta_right(k)*phix_1d(k,jpp)*rintfac_right(k) + &
                ( reta_right(k)**2 + zeta_right(k)**2 )**(-0.5_rk) *phi_1d(k,jpp) )

           integrandRv_z_S(k) = phix_1d(k,ipp)*phix_1d(k,jpp) / sqrt( reta_right(k)**2 + zeta_right(k)**2 ) *rintfac_right(k) - &
                zeta_right(k)**2 *phix_1d(k,ipp)*phix_1d(k,jpp) / ( reta_right(k)**2 + zeta_right(k)**2 )**1.5_rk *rintfac_right(k)


        end do
        sj(i,j,NNz,NNr) = sj(i,j,NNz,NNr) - M1*gaussian_quadrature_1d(integrandReta_r_S)
        sj(i,j,NNz,NNz) = sj(i,j,NNz,NNz) - M1*gaussian_quadrature_1d(integrandReta_z_S)

        sj(i,j,NNr,NNr) = gaussian_quadrature_1d(integrandRsi_r_S)     !directly replace Rsi_V with Rsi_S, viz KBC
        sj(i,j,NNr,NNz) = gaussian_quadrature_1d(integrandRsi_z_S)
        sj(i,j,NNr,NNu) = gaussian_quadrature_1d(integrandRsi_u_S)
        sj(i,j,NNr,NNv) = gaussian_quadrature_1d(integrandRsi_v_S)

        sj(i,j,NNu,NNr) = sj(i,j,NNu,NNr) + gaussian_quadrature_1d(integrandRu_r_S)
        sj(i,j,NNu,NNz) = sj(i,j,NNu,NNz) + gaussian_quadrature_1d(integrandRu_z_S)

        sj(i,j,NNv,NNr) = sj(i,j,NNv,NNr) + gaussian_quadrature_1d(integrandRv_r_S)
        sj(i,j,NNv,NNz) = sj(i,j,NNv,NNz) + gaussian_quadrature_1d(integrandRv_z_S)

     else
        sj(i,j,NNr,:) = 0.0_rk        !d Rsi/ d xj = 0.0_rk other than j = 3,6,9

     end if
  end if



end subroutine SI_in_sj


